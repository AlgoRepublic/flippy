<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:paper="components.*" creationComplete="init()" borderColor="#B2AFAF" borderThickness="1" borderStyle="solid" height="160">
	<mx:Script>
		<![CDATA[
			import mx.controls.Image;
			import mx.controls.NumericStepper;
			import mx.events.ListEvent;
			import mx.controls.ComboBox;
			import mx.events.NumericStepperEvent;
			import mx.events.ColorPickerEvent;
			import mx.core.UIComponent;
			import mx.collections.ArrayCollection;
			import mx.controls.Button;
			private var tools:ArrayCollection;
			private var thicknessOptions:Array = [1, 2, 3, 4, 5, 6, 7, 8, 9, 10];
			
			// shared object
			private var nc:NetConnection = null;
			private var mySo:SharedObject = null;
			private var rtmpUrl:String = "rtmp://202.158.39.178/flippy";
			private var _sessionId:int;
			
			private var _paperMask:Sprite = new Sprite();
						
			public function set serverUrl(url:String):void
			{
				rtmpUrl = url;
			}
			
			public function get serverUrl():String
			{
				return rtmpUrl;
			}
			
			public function set sessionId(val:int):void
			{
				_sessionId = val;
			}
			
			public function get sessionId():int
			{
				return _sessionId;
			}
			
			public function connect():void
			{
				
				nc = new NetConnection();
				nc.addEventListener(NetStatusEvent.NET_STATUS, netStatusHandler);
				
				trace("Connecting to server using '" + rtmpUrl + "' ...");
				nc.connect(rtmpUrl);
				
				trace("Getting remote object from " + nc.uri);
				mySo = SharedObject.getRemote("whiteboard", nc.uri);
				mySo.addEventListener(SyncEvent.SYNC, soSyncHandler);
				
			}
			
			public function disconnect():void
			{
				if (mySo != null)
				{
					mySo.close();
				}
				if (nc != null)
				{
					nc.close();
				}
			}
			
			private function netStatusHandler(event:NetStatusEvent):void
			{
				trace("nc:" + event.info.code + " - " + event.info.description);
				if (event.info.code == "NetConnection.Connect.Success")
				{
					trace("Connected");
					
					function resultHandler(data:Object):void
					{
						trace("Succeed join session " + _sessionId);
						trace("Create connection to shared object...");
						mySo.connect(nc);
						drawPaper.drawablesSharedObject = mySo;
					}
					
					function statusHandler(info:Object):void
					{
						trace("Failed joining session " + _sessionId + ":" + info);
					}
					trace("Joining to session " + _sessionId);
					nc.call("joinSession", new Responder(resultHandler, statusHandler), _sessionId);
				}
			}
			
			private function soSyncHandler(event:SyncEvent):void
			{
				trace("Sync Event");
				var changeList:Array = event.changeList;
				for (var i:int = 0; i < changeList.length; i++)
				{
					var info:Object = changeList[i];
					trace("changed object [" + i + "]:name='" + info.name + "', code='" + info.code + "'"); 
				}
				
				for (var p:Object in mySo.data)
				{
					trace("slotName='" + p + "', slotValue='" + mySo.data[p] + "'");
				}
				drawPaper.repaintDrawables();
			}
			
			
			private function init():void {
				tools = new ArrayCollection([rectangleTool, ovalTool, lineTool, penTool]);
				
				// initialize paper mask
				_paperMask.graphics.clear();
				_paperMask.graphics.beginFill(0xFF0000, 1);
				_paperMask.graphics.drawRect(0, 0, drawPaper.width, drawPaper.height);
				_paperMask.graphics.endFill();
				drawPaperBox.rawChildren.addChild(_paperMask);
				drawPaper.mask = _paperMask;
				
				connect();
			}
			
			private function paperResizeHandler():void
			{
				trace("Draw paper resized");
				_paperMask.graphics.clear();
				_paperMask.graphics.beginFill(0xFF0000, 1);
				_paperMask.graphics.drawRect(0, 0, drawPaperBox.width, drawPaperBox.height);
				_paperMask.graphics.endFill();
			}
			
			private function selectATool(tool:Button):void {
				var arr:Array = tools.source;
				for (var i:int = 0; i < arr.length; i++) {
					if (arr[i] != tool) {
						(arr[i] as Button).selected = false;
					}
				}
				tool.selected = true;
				drawPaper.tool = tool.id;
			}
			
			private function strokeColorChanged(event:ColorPickerEvent):void
			{
				drawPaper.strokeColor = event.color;
			}
			
			private function fillColorChanged(event:ColorPickerEvent):void
			{
				drawPaper.fillColor = event.color;
			}
			
			private function fillAlphaChanged(event:NumericStepperEvent):void
			{
				drawPaper.fillAlpha = event.value / 100;
			}
			
			private function strokeThicknessChanged(event:NumericStepperEvent):void
			{
				drawPaper.strokeThickness = event.value;
				
			}
		]]>
	</mx:Script>
	
	<mx:VBox width="100%" height="100%">
		<mx:ApplicationControlBar width="100%">
			<mx:HBox>
				<mx:Label text="Stroke"/>
				<mx:ColorPicker change="strokeColorChanged(event)"/>
			</mx:HBox>
			<mx:Spacer/>
			<mx:HBox>
				<mx:Label text="Fill"/>
				<mx:ColorPicker change="fillColorChanged(event)"/>
				<mx:CheckBox id="fillColorCheck" change="drawPaper.opaque = !fillColorCheck.selected"/>
			</mx:HBox>
			<mx:Spacer/>
			<mx:HBox>
				<mx:Label text="Alpha"/>
				<mx:NumericStepper width="53" value="100" stepSize="1" minimum="0" maximum="100" change="fillAlphaChanged(event)"/>
			</mx:HBox>
			<mx:Spacer/>
			<mx:HBox>
				<mx:Label text="Thickness"/>
				<mx:NumericStepper width="53" value="1" stepSize="1" minimum="1" maximum="10" change="strokeThicknessChanged(event)"/>
			</mx:HBox>
			<mx:Button id="rectangleTool" selected="true" click="selectATool(rectangleTool)" icon="@Embed(source='../assets/rectangle tool.svg')" height="29" label="Rectange" textAlign="left"/>
			<mx:Button id="ovalTool" click="selectATool(ovalTool)" height="29" icon="@Embed(source='../assets/oval tool.svg')" label="Oval" textAlign="left"/>
			<mx:Button id="lineTool" click="selectATool(lineTool)" height="29" icon="@Embed(source='../assets/line tool.svg')" label="Line" textAlign="left"/>
			<mx:Button id="penTool" click="selectATool(penTool)" height="29" icon="@Embed(source='../assets/pen tool.svg')" label="Pen" textAlign="left"/>
			<mx:Button id="clearTool" click="drawPaper.clear()" height="29" icon="@Embed(source='../assets/eraser tool.svg')" label="Erase All" textAlign="left"/>
		</mx:ApplicationControlBar>
		<mx:HBox width="100%">
			<mx:Label text="Image URL:"/>
			<mx:TextInput id="imageUrl" width="100%"/>
			<mx:Button label="Show"/>
		</mx:HBox>
		<mx:HBox id="drawPaperBox" width="100%" height="100%" horizontalScrollPolicy="auto" verticalScrollPolicy="auto" clipContent="true" resize="paperResizeHandler()">
			<paper:Paper id="drawPaper" width="100%" height="100%" focusEnabled="true"/>
		</mx:HBox>
	</mx:VBox>
		
</mx:HBox>
